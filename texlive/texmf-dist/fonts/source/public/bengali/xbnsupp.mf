%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% asextra.mf: METAFONT definitions for Assamese extension characters
% Created by: Anshuman Pandey <apandey@u.washington.edu>
% Last modified: Mar 27 2002
%
% apandey 2002.03.27 added "Assamese ra" at position 114 "r"
% apandey 2002.03.27 added "Assamese va" at position 118 "v"
% apandey 2002.03.27 added "Bengali lig thba" at position 76 "L"
% apandey 2002.03.27 added "Bengali lig ndba" at position 80 "P"
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

input bnmacro;


% th + ba
newchar(76,0.48,"th + b");
ha(1/10w,1/4h+0.2pt,2/3w+0.1pt,3/5h-0.1pt);
pickup pencircle scaled 0.4pt;
draw (5/6w,1/4h)..(5/6w,3/4h+0.2pt);
draw(5/6w,3/4h)..(w,3/4h);
ba(1/3w,-1/3d,1/2w,33/80h);
endchar;


% n + d + ba
newchar(80,0.66,"n + d + b");
matra(w,h);
na(1/10w,(3/10+1/16)*h,(1/2-1/10)*w,5/16h,0.2);
da(1/2w,1/5h,1/2w-0.9pt,3/7h,0);
ba(21/40w+0.03pt,-1/4d,1/3w,8/20h);
endchar;


% Assamese ra
beginchar(114,0.52stwd#,stht#,stdp#); "The letter Assamese ra"; % "r"
pickup pencircle scaled penth#;
z1 = (0,3/4h); z2=(w,3/4h);
draw z1..z2;
z5 = (w-0.9pt,13/20h);
z5r = z5 + (0,penth#/2); z5l = z5 - (0,penth#/2);
z6 = (1/6w,8/21h);
numeric len; len = 1/2(penth#/(sqrt 2));
z6l = z6 + (len,-len); z6r = z6 - (len,-len);
penstroke z5e{z6-z5 rotated -8}..z6e;
bot z4 = z4l + (0.1pt,0) = z4r - (0.1pt,0) = (w-0.9pt,0);
z7 - z6 = (0.2pt,-0.2pt);
z7l = z7 + (len, -len); z7r = z6r;
penstroke z7e{z4-z7 rotated 120}.. tension 1.5 .. z4e;
top z3 = (w-0.9pt,3/4h);
draw z3..z4;
pickup pencircle xscaled 0.3pt yscaled 0.6pt rotated 50;
rt z8 = (5/6w,1/4h);
z9 = (1/2w,43/80h);
%draw (z9+(0,0.1pt))--z8;
draw z8--z9;
endchar;


% Assamese va
beginchar(118,0.52stwd#,stht#,stdp#); "The letter Assamese va"; % "v"
pickup pencircle scaled penth#;
z1 = (0,3/4h); z2=(w,3/4h);
draw z1..z2;
z5 = (w-0.9pt,13/20h);
z5r = z5 + (0,penth#/2); z5l = z5 - (0,penth#/2);
z6 = (1/6w,8/21h);
numeric len; len = 1/2(penth#/(sqrt 2));
z6l = z6 + (len,-len); z6r = z6 - (len,-len);
penstroke z5e{z6-z5 rotated -8}..z6e;
bot z4 = z4l + (0.1pt,0) = z4r - (0.1pt,0) = (w-0.9pt,0);
z7 - z6 = (0.2pt,-0.2pt);
z7l = z7 + (len, -len); z7r = z6r;
penstroke z7e{z4-z7 rotated 120}.. tension 1.5 .. z4e;
top z3 = (w-0.9pt,3/4h);
draw z3..z4;
pickup pencircle xscaled 0.3pt yscaled 0.6pt rotated 50;
rt z8 = (2/3w,1/11h);
z9 = (1/4w,1/4h);
draw (z9+(0,0.1pt))--z8;
endchar;

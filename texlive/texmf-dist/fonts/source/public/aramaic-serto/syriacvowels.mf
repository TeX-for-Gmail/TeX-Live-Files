% Syriac Vowels 10 point Normal Face `syriacvowels.mf'
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Designed and (c) by Johannes Heinecke
%                     <johannes(dot)heinecke(at)wanadoo(dot)fr>
% Please send any improvements, corrections, suggestions, enhancements etc.
% to the Author!
%
% Version 0.5 as of 10th November 2003
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% Vowels

pickup pencircle xscaled .8basestroke yscaled 2basestroke rotated 20; 
chaldeanvowels_pen:=savepen;

pickup pencircle xscaled .9basestroke yscaled 1.3basestroke rotated 10; 
chaldeanvowels2_pen:=savepen;

if unknown sertomacros: input sertomacros fi
sertochar "peto.ho a";
beginchar(1, 3hu#, 5vu#, 0);
    pickup vowel_pen;
    z11 = (0, 3vu);
    z12 = (x1+3hu, y1+3vu);
    z1 = (.1[z11,z12]);
    z2 = (.9[z11,z12]);
    z3 = (.3[z11,z12]);
    z4 = (.7[z11,z12]);
    %z5 = (x1+.5hu, y2);
    z16 = (.5[z11,z12]); % Hilfspunkt
    z6 = z16 + 2hu*dir(angle(z2-z1)+90);
    draw z1--z2;
    draw z3--z6--z4;
    penlabels(1,2,3,4,6)
endchar;


sertochar "revo.so e";
beginchar(2, 3hu#, 6vu#, 0);
   pickup vowel_pen;
   z1 = (0, 4vu);
   z2 = (x1+2.2hu, y1+vu);
   z3 = (x1+3hu, y1-vu);
   z4 = z2 + whatever*dir(-110);
   z4 = whatever[z1,z3];

   draw z1..z2..z3;
   draw z2--z4;

   penlabels(1,2,3,4);
endchar;


sertochar ".hevo.so i";
beginchar(3, 3hu#, 6vu#, 0);
   pickup vowel_pen;

   z1 = (.8hu, 3vu);
   z2 = z1 + (2.9hu, 1.5vu);
   z3 = z1 + (-.8hu, 1vu);
   z4 = z3 + (3hu, 1.5vu);
   z5 = .4[z3, z4];
   z6 = .5[z1, z2];

   draw z1--z2;
   draw z3--z4;
   draw z5--z6;

   penlabels(1,2,3,4,5,6);
endchar;

sertochar "zeqopo o";
beginchar(4, 3hu#, 6vu#, 0);
   pickup vowelII_pen;

   z1 = (0, 3vu);
   z2 = z1 + (2hu, 3vu);
   z3 = z1 + (1.3hu, 1.5vu);

   draw z1{dir 30}..{dir 135}z2..{right}z3;
   penlabels(1,2,3);
endchar;

sertochar "`e.so.so u";
beginchar(5, 3hu#, 8vu#, 0);
   pickup vowel_pen;

   z1 = (0, 5vu);
   z2 = z1 + (3hu, 3vu);
   z3 = .47[z1, z2];
   z4 = z1 + (2.5hu, -.5vu);

   draw z1--z2;
   draw z3--z4;

   pickup vowelIII_pen;
   z5 = z1 + (1.4hu, 0);
   z6 = z5 - (.5hu, .6vu);

   draw z5..z6..cycle;

   penlabels(range 1 thru 6)
endchar;






sertochar "qu^s^soyo (hard sign)";
beginchar(6, 1hu#, 4vu#, 0);
    z1 = (.5hu, 4vu);
    sertodot(1, 1.2);
endchar;


sertochar "peto.ho a invers";
beginchar(17, 3hu#, 5vu#, 0);
    pickup vowel_pen;
    z11 = (0, 1vu);
    z12 = (x1+3hu, y1+3vu);
    z1 = (.1[z11,z12]);
    z2 = (.9[z11,z12]);
    z3 = (.3[z11,z12]);
    z4 = (.7[z11,z12]);
    %z5 = (x1+.5hu, y2);
    z16 = (.5[z11,z12]); % Hilfspunkt
    z6 = z16 - 2hu*dir(angle(z2-z1)+90);
    draw z1--z2;
    draw z3--z6--z4;
    penlabels(1,2,3,4,6)
endchar;


sertochar "revo.so e invers";
beginchar(18, 3hu#, 6vu#, 0);
   pickup vowel_pen;
   z1 = (0, 4vu);
   z2 = (x1+hu, y1-2.2vu);
   z3 = (x1+3hu, y1-vu);
   z4 = z2 + whatever*dir(-110);
   z4 = whatever[z1,z3];

   draw z1..z2..z3;
   draw z2--z4;

   penlabels(1,2,3,4);
endchar;


sertochar ".hevo.so i";
beginchar(19, 3hu#, 4vu#, 0);
   pickup vowel_pen;

   z1 = (.8hu, 1vu);
   z2 = z1 + (2.9hu, 1.5vu);
   z3 = z1 + (-.8hu, 1vu);
   z4 = z3 + (3hu, 1.5vu);
   z5 = .4[z3, z4];
   z6 = .5[z1, z2];

   draw z1--z2;
   draw z3--z4;
   draw z5--z6;

   penlabels(1,2,3,4,5,6);
endchar;

sertochar "zeqopo o invers";
beginchar(20, 3hu#, 6vu#, 0);
   pickup vowelII_pen;

   z1 = (2.5hu, 5vu);
   z2 = z1 - (2hu, 3vu);
   z3 = z1 - (1.3hu, 1.5vu);

   draw z1{dir -150}..{dir -45}z2..{left}z3;
   penlabels(1,2,3);
endchar;

sertochar "`e.so.so u invers";
beginchar(21, 3hu#, 8vu#, 0);
   pickup vowel_pen;

   z1 = (3hu, 5vu);
   z2 = z1 - (3hu, 3vu);
   z3 = .47[z1, z2];
   z4 = z1 - (2.5hu, -.5vu);

   draw z1--z2;
   draw z3--z4;

   pickup vowelIII_pen;
   z5 = z1 - (1.4hu, 0);
   z6 = z5 + (.5hu, .6vu);

   draw z5..z6..cycle;

   penlabels(range 1 thru 6)
endchar;





% Chaldean vowels
sertochar "zeqopo =a (chaldean)";
beginchar(oct"014", 2hu#, 5vu#, 0);
   pickup chaldeanvowels_pen;

   lft bot z1 = (0, 2vu);
   z2 = z1 + (1.2hu, 1.3vu);
   drawdot z1;
   drawdot z2;
   penlabels(1,2);
endchar;

sertochar "revo.so e (chaldean)";
beginchar(oct"32", 3hu#, 6vu#, 0);
   pickup chaldeanvowels_pen;
   lft z1 = (0, 2vu);
   z2 = z1 + (1.3hu, 1.6vu);
   drawdot z1;
   drawdot z2;

   penlabels(1,2);
endchar;

sertochar ".hevo.so i (chaldean, also used for u (waw + dot))";
beginchar(oct"33", 2hu#, 4vu#, 0);
   lft z1 = (1hu, 2vu);
   pickup chaldeanvowels2_pen;
   drawdot z1;
   penlabels(1,2);
endchar;

sertochar " o (chaldean, also used for a)";
beginchar(oct"15", 2hu#, 3vu#, 0);
   lft z1 = (1hu, 3vu);
   pickup chaldeanvowels2_pen;
   drawdot z1;
   penlabels(1,2);
endchar;


endinput;